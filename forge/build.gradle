plugins {
    id 'com.github.johnrengelman.shadow'
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "mixin.music_delay_reducer.json"
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

configurations.configureEach {
    resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:5.0.4")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    from project(':').file('LICENSE.md')

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

unifiedPublishing {
    project {
        displayName = "[Forge ${project.minecraft_version}] Music Delay Reducer ${project.version}"
        version = "${project.version}"
        releaseType = 'release'
        gameVersions = ["${project.minecraft_version}"]
        gameLoaders = ['forge']
        changelog = project(':').file('changelog.md').text

        mainPublication tasks.remapJar

        if (curseforgeApiKey != null) {
            curseforge {
                token = curseforgeApiKey
                id = '1001877'
            }
        }

        if (modrinthApiKey != null) {
            modrinth {
                token = modrinthApiKey
                id = 'CTJn5mVm'
            }
        }
    }
}